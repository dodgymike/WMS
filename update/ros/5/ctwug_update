:local name [/system identity get name]
:local serial [/system routerboard get serial-number]
:local rosver [/system resource get version]
:local cpu [/system resource get cpu]
:local cpufreq [/system resource get cpu-frequency]
:local arch [/system resource get architecture-name]
:local board [/system resource get board-name]
:local firmware [/system routerboard get current-firmware]
:local contact [/snmp get contact]
:local ver %ver%
:local curver 0
:local upgrade 0
:local upoln 0
:local rscript "ctwug_update.rsc"

/file
:foreach n in [find where name~"^ctwug_.*\\.rsc"] do={
  :log info ("ctwug_update: removing file " . [get $n name])
  remove $n
}

# This scheduler gets created at the end of an upgrade, to signal a successful upgrade
/system scheduler
:foreach n in [find where name="ctwug_update_temp"] do={
  :set upgrade 1
  :log info ("ctwug_update: removing scheduler " . [get $n name])
  remove $n
}

/system script environment
:foreach n in [find where name="ctwugcurver"] do={
  :set curver [get $n value]
}

:if ([/system script find name=ctwug_update] != "" ) do={
  :local upolicy [/system script get ctwug_update policy]
  :for i from=0 to=([:len $upolicy] -1) do={
    :local pflag [:pick $upolicy $i]
    :if ($pflag = "reboot") do={:set upoln ($upoln+1)}
    :if ($pflag = "read") do={:set upoln ($upoln+2)}
    :if ($pflag = "write") do={:set upoln ($upoln+4)}
    :if ($pflag = "policy") do={:set upoln ($upoln+8)}
    :if ($pflag = "test") do={:set upoln ($upoln+16)}
    :if ($pflag = "password") do={:set upoln ($upoln+32)}
    :if ($pflag = "sniff") do={:set upoln ($upoln+64)}
    :if ($pflag = "sensitive") do={:set upoln ($upoln+128)}
  }
}

:local path ("api/update?serial=".$serial."&ver=".$ver."&rosver=".$rosver."&cpu=".$cpu."&cpufreq=".$cpufreq."&arch=".$arch."&board=".$board."&firmware=".$firmware."&policy=".$upoln."&name=".$name)
:if ([:len $contact] > 5) do={
  :set path ($path."&contact=".$contact)
}
:if ($curver != 0) do={
  :set path ($path."&curver=".$curver)
}
:if ($upgrade = 1) do={
  :set path ($path."&upgrade=1")
}
:local apipath
:local fchar
:for i from=0 to=( [:len $path] - 1) do={
  :set fchar [:pick $path $i]
  :if ($fchar = " ") do={
    :set fchar "%20"
  }
  :set apipath ($apipath.$fchar)
}

/tool fetch address=wms.ctwug.za.net host=wms.ctwug.za.net src-path=$apipath dst-path=$rscript mode=http
:delay 3
:local temp [/file get $rscript size]
:if ( $temp > 2) do={
  /import $rscript
}
