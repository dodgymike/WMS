:local rosver [:pick [/system resource get version] 0]
:local qdisabled yes

/queue simple
:foreach n in [find where comment~"^AUTO.*"] do={
  :if (![get $n disabled]) do={
    :set qdisabled no
  }
  :log info ("ctwug_qos: removing simple queue " . [get $n name])
  remove $n
}


/interface
:local i 0
:foreach n in [find where comment~"^client;[0-9]+;"] do={
  :set i ($i+1)
  :local com [get $n comment]
  :local iface [get $n name]
  :local p1 ([:find $com ";"] + 1)
  :local p2 [:find $com ";" $p1]
  :local limit ("0/".([:pick $com $p1 $p2]*1000))
  :local com ("AUTO ".$iface." BULK")
  :local name ("ctwug_bulk" . $i)
  :local qAdd
  :if ($rosver = "6") do={
    :set qAdd [:parse "/queue simple add target=\"$iface\" name=$name packet-marks=BULK comment=\"$com\" max-limit=$limit disabled=yes"]
  } else={
    :set qAdd [:parse "/queue simple add interface=\"$iface\" name=$name packet-marks=BULK comment=\"$com\" max-limit=$limit direction=download disabled=yes"]
  }
  $qAdd
  :log info ("ctwug_qos: adding simple queue " . $name . " for interface " . $iface)
}

/queue simple
:foreach n in [find where name~"^ctwug_" && disabled!=$qdisabled] do={
  set $n disabled=$qdisabled
}
